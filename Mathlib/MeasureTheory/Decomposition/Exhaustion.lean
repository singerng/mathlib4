/-
Copyright (c) 2024 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.MeasureTheory.Measure.Typeclasses
import Mathlib.MeasureTheory.Decomposition.ExhaustionFun

/-!
# Method of exhaustion

For `p : Set Î± â†’ Prop` with `hp_empty : p âˆ…` and a finite measure `Î¼` on `Î±`, we construct
a measurable set `Î¼.maximalSet p hp_empty` which has maximal `Î¼`-measure among all measurable sets
with property `p`.
That is, `Î¼ (Î¼.maximalSet p hp_empty) = â¨† (s) (_ : MeasurableSet s) (_ : p s), Î¼ s`.

That set is built as a supremum of a sequence of sets indexed by `â„•`, with measure converging
to the supremum.

## Main definitions

* `MeasureTheory.Measure.maximalSet`: a measurable set which has maximal `Î¼`-measure among
  all measurable sets that satisfy a property `p : Set Î± â†’ Prop`.

## Main statements

* `measurableSet_maximalSet`: `maximalSet` is a measurable set.
* `prop_maximalSet`: `maximalSet` satisfies the property `p` used to define it.
* `measure_maximalSet`: the measure of `maximalSet` is equal to the supremum of the measure of
  the measurable sets that satisfy `p`.
* `not_prop_of_subset_compl_maximalSet` : if a subset `s` of the complement of `maximalSet` has
  non-zero measure, it does not have property `p`.

## References

* [P. R. Halmos, *Measure theory*, 17.3 and 30.11][halmos1950measure]

-/

open scoped ENNReal Topology

open Filter

namespace MeasureTheory

variable {Î± : Type*} {mÎ± : MeasurableSpace Î±} {Î¼ : Measure Î±} [IsFiniteMeasure Î¼]
  {p : Set Î± â†’ Prop} {s : Set Î±}

/-- Let `p : Set Î± â†’ Prop` be a predicate on sets and let `C` be the supremum of `Î¼ s` over
all measurable sets `s` with property `p s`. `C` is finite since `Î¼` is a finite measure.
Then there exists a measurable set `t` with `p t` such that `Î¼ t â‰¥ C - 1/n`. -/
lemma exists_set_measure_ge (Î¼ : Measure Î±) [IsFiniteMeasure Î¼]
    (p : Set Î± â†’ Prop) (hp_exists : âˆƒ s, MeasurableSet s âˆ§ p s) :
    âˆƒ (f : â„• â†’ Set Î±), (âˆ€ n, MeasurableSet (f n)) âˆ§ (âˆ€ n, p (f n))
      âˆ§ Monotone (fun n â†¦ Î¼ (f n))
      âˆ§ Tendsto (fun n â†¦ Î¼ (f n)) atTop (ğ“ (â¨† (s) (_ : MeasurableSet s) (_ : p s), Î¼ s)) := by
  obtain âŸ¨f, hf_mono, hf_tendsto, hfâŸ© :=
    exists_seq_tendsto_iSup hp_exists (OrderTop.bddAbove _) (F := Î¼)
  choose hf_meas hfp using hf
  change Tendsto (fun n â†¦ Î¼ (f n)) atTop (ğ“ (â¨† a âˆˆ {x | MeasurableSet x âˆ§ p x}, Î¼ a))
    at hf_tendsto
  simp only [Set.mem_setOf_eq, iSup_and] at hf_tendsto
  exact âŸ¨f, hf_meas, hfp, hf_mono, hf_tendstoâŸ©

open Classical in
/-- A measurable set such that `p (Î¼.maximalSet p hp_empty)` and the measure
`Î¼ (Î¼.maximalSet p hp_empty)` is maximal among such sets. -/
def Measure.maximalSet (Î¼ : Measure Î±) [IsFiniteMeasure Î¼] (p : Set Î± â†’ Prop) :
    Set Î± :=
  if hp_exists : âˆƒ s, MeasurableSet s âˆ§ p s
    then â‹ƒ n, (exists_set_measure_ge Î¼ p hp_exists).choose n else âˆ…

lemma maximalSet_of_exists (hp_exists : âˆƒ s, MeasurableSet s âˆ§ p s) :
    Î¼.maximalSet p = â‹ƒ n, (exists_set_measure_ge Î¼ p hp_exists).choose n :=
  dif_pos hp_exists

lemma maximalSet_of_not_exists (hp_empty : Â¬ âˆƒ s, MeasurableSet s âˆ§ p s) :
    Î¼.maximalSet p = âˆ… := dif_neg hp_empty

lemma measurableSet_maximalSet (p : Set Î± â†’ Prop) :
    MeasurableSet (Î¼.maximalSet p) := by
  by_cases hp_exists : âˆƒ s, MeasurableSet s âˆ§ p s
  Â· rw [maximalSet_of_exists hp_exists]
    exact MeasurableSet.iUnion (exists_set_measure_ge Î¼ p hp_exists).choose_spec.1
  Â· rw [maximalSet_of_not_exists hp_exists]
    exact .empty

lemma prop_maximalSet (Î¼ : Measure Î±) [IsFiniteMeasure Î¼]
    (p : Set Î± â†’ Prop) (hp_exists : âˆƒ s, MeasurableSet s âˆ§ p s)
    (hp_iUnion : âˆ€ (t : â„• â†’ Set Î±) (_ : âˆ€ n, MeasurableSet (t n)) (_ : âˆ€ n, p (t n)),
      p (â‹ƒ n, t n)) :
    p (Î¼.maximalSet p) := by
  rw [maximalSet_of_exists hp_exists]
  exact hp_iUnion _ (exists_set_measure_ge Î¼ p hp_exists).choose_spec.1
    (exists_set_measure_ge Î¼ p hp_exists).choose_spec.2.1

/-- `Î¼.maximalSet p hp_empty` has maximal `Î¼`-measure among all measurable sets `s` with `p s`. -/
lemma measure_maximalSet (Î¼ : Measure Î±) [IsFiniteMeasure Î¼] (p : Set Î± â†’ Prop)
    (hp_iUnion : âˆ€ (t : â„• â†’ Set Î±) (_ : âˆ€ n, MeasurableSet (t n)) (_ : âˆ€ n, p (t n)),
      p (â‹ƒ n, t n)) :
    Î¼ (Î¼.maximalSet p) = â¨† (s) (_ : MeasurableSet s) (_ : p s), Î¼ s := by
  by_cases hp_exists : âˆƒ s, MeasurableSet s âˆ§ p s
  swap
  Â· rw [maximalSet_of_not_exists hp_exists, measure_empty]
    symm
    simp only [ENNReal.iSup_eq_zero]
    push_neg at hp_exists
    intro s hs hps
    exact absurd hps (hp_exists s hs)
  simp_rw [maximalSet_of_exists hp_exists]
  apply le_antisymm
  Â· refine (le_iSup (f := fun _ â†¦ _) (prop_maximalSet Î¼ p hp_exists hp_iUnion)).trans ?_
    convert le_iSupâ‚‚ (f := fun s _ â†¦ â¨† (_ : p s), Î¼ s) (Î¼.maximalSet p)
      (measurableSet_maximalSet p)
    rw [maximalSet_of_exists hp_exists]
  Â· exact le_of_tendsto' (exists_set_measure_ge Î¼ p hp_exists).choose_spec.2.2.2
      (fun _ â†¦ measure_mono (Set.subset_iUnion _ _))

lemma not_prop_of_disjoint_maximalSet (Î¼ : Measure Î±) [IsFiniteMeasure Î¼] (p : Set Î± â†’ Prop)
    (hp_iUnion : âˆ€ (t : â„• â†’ Set Î±) (_ : âˆ€ n, MeasurableSet (t n)) (_ : âˆ€ n, p (t n)),
      p (â‹ƒ n, t n))
    (hs : MeasurableSet s) (hs_disj : Disjoint (Î¼.maximalSet p) s) (hÎ¼s : Î¼ s â‰  0) :
    Â¬ p s := by
  by_cases hp_exists : âˆƒ s, MeasurableSet s âˆ§ p s
  swap; Â· push_neg at hp_exists; exact hp_exists s hs
  intro hsp
  have h_lt : Î¼ (Î¼.maximalSet p) < Î¼ (Î¼.maximalSet p âˆª s) := by
    rw [measure_union hs_disj hs]
    exact ENNReal.lt_add_right (measure_ne_top _ _) hÎ¼s
  have hp_union {s t} (hs : MeasurableSet s) (ht : MeasurableSet t) (hps : p s) (hpt : p t) :
      p (s âˆª t) := by
    let ts : â„• â†’ Set Î± := fun n â†¦ if n = 0 then s else t
    have : s âˆª t = â‹ƒ n, ts n := by
      simp only [ts, Set.iUnion_ite, Set.iUnion_iUnion_eq_left]
      congr with x
      simp only [Set.mem_iUnion, exists_prop, exists_and_right, iff_and_self]
      exact fun _ â†¦ âŸ¨1, by simpâŸ©
    rw [this]
    refine hp_iUnion ts (fun n â†¦ ?_) (fun n â†¦ ?_)
    Â· cases n with
      | zero => simp only [â†“reduceIte, ts, hs]
      | succ n =>
          simp only [AddLeftCancelMonoid.add_eq_zero, one_ne_zero, and_false, â†“reduceIte, ts, ht]
    Â· cases n with
      | zero => simp only [â†“reduceIte, ts, hps]
      | succ n =>
          simp only [AddLeftCancelMonoid.add_eq_zero, one_ne_zero, and_false, â†“reduceIte, ts, hpt]
  have h_le : Î¼ (Î¼.maximalSet p âˆª s) â‰¤ Î¼ (Î¼.maximalSet p) := by
    conv_rhs => rw [measure_maximalSet _ _ hp_iUnion]
    refine (le_iSup
      (f := fun (_ : p (Î¼.maximalSet p âˆª s)) â†¦ _) ?_).trans ?_
    Â· exact hp_union (measurableSet_maximalSet p) hs (prop_maximalSet Î¼ p hp_exists hp_iUnion) hsp
    Â· exact le_iSupâ‚‚ (f := fun s _ â†¦ â¨† (_ : p _), Î¼ s)
        (Î¼.maximalSet p âˆª s) ((measurableSet_maximalSet p).union hs)
  exact h_lt.not_le h_le

end MeasureTheory
