/-
Copyright (c) 2023 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import Mathlib.Analysis.InnerProductSpace.PiL2
import Mathlib.Analysis.MeanInequalitiesPow
import Mathlib.Data.Nat.Choose.Multinomial
import Mathlib.Tactic.Positivity.Finset

/-!
# The Marcinkiewicz-Zygmund inequality

This file proves the Marcinkiewicz-Zygmund inequality. This is the statement that, for a random
variable `f` valued in a real inner product space, the $$L^p$$-norm of the sum of `n` samples of `f`
is bounded by the $$L^p$$-norm of `f` up to some multiplicative constant.

## TODO

* We currently only prove the inequality for `p = 2 * m` an even natural number. The general `p`
  case can be obtained from this specific one by nesting of Lp norms.
* We currently only prove the case of a uniformly distributed discrete random variable. This needs
  to be generalised! (and when it is, this file should move out of the `Algebra.Discrete`) folder.
-/

open Finset Fintype Nat Real
variable {Î± : Type*} {n : Type*} [Fintype n] {A : Finset Î±} {m : â„•}

private lemma step_seven {f : Î± â†’ â„} {a b : n â†’ Î±} :
    m ^ m * (âˆ‘ i, (f (a i) - f (b i)) ^ 2 : â„) ^ m â‰¤
      m ^ m * 2 ^ m * (âˆ‘ i, (f (a i) ^ 2 + f (b i) ^ 2)) ^ m := by
  rw [â† mul_pow, â† mul_pow, â† mul_pow, mul_assoc, mul_sum _ _ (2 : â„)]
  gcongr
  exact add_sq_le.trans_eq (by simp)

private lemma step_eight {f : Î± â†’ â„} {a b : n â†’ Î±} :
    m ^ m * 2 ^ m * (âˆ‘ i, (f (a i) ^ 2 + f (b i) ^ 2) : â„) ^ m â‰¤
      m ^ m * 2 ^ (m + (m - 1)) *
        ((âˆ‘ i, f (a i) ^ 2) ^ m + (âˆ‘ i, f (b i) ^ 2) ^ m) := by
  rw [pow_add, â† mul_assoc _ _ (2 ^ _), mul_assoc _ (2 ^ (m - 1)), sum_add_distrib]
  gcongr
  exact add_pow_le (by positivity) (by positivity) m

variable [DecidableEq n]

local notation:70 s:70 " ^^ " n:71 => Fintype.piFinset fun _ : n â†¦ s

private lemma step_one (hA : A.Nonempty) (f : Î± â†’ â„) (a : n â†’ Î±)
    (hf : âˆ€ i, âˆ‘ a in A ^^ n, f (a i) = 0) :
    |âˆ‘ i, f (a i)| ^ (m + 1) â‰¤
      (âˆ‘ b in A ^^ n, |âˆ‘ i, (f (a i) - f (b i))| ^ (m + 1)) / A.card ^ card n := by
  let B := A ^^ n
  calc
    |âˆ‘ i, f (a i)| ^ (m + 1)
      = |âˆ‘ i, (f (a i) - (âˆ‘ b in B, f (b i)) / B.card)| ^ (m + 1) := by
      simp only [hf, sub_zero, zero_div]
    _ = |(âˆ‘ b in B, âˆ‘ i, (f (a i) - f (b i))) / B.card| ^ (m + 1) := by
      simp [sum_comm (s := B), â† sum_div, â† mul_sum, sub_div, B, hA.ne_empty]
    _ = |âˆ‘ b in B, âˆ‘ i, (f (a i) - f (b i))| ^ (m + 1) / B.card ^ (m + 1) := by
      rw [abs_div, div_pow, Nat.abs_cast]
    _ â‰¤ (âˆ‘ b in B, |âˆ‘ i, (f (a i) - f (b i))|) ^ (m + 1) / B.card ^ (m + 1) := by
      gcongr; exact abs_sum_le_sum_abs _ _
    _ = (âˆ‘ b in B, |âˆ‘ i, (f (a i) - f (b i))|) ^ (m + 1) / B.card ^ m / B.card := by
      rw [div_div, â† pow_succ]
    _ â‰¤ (âˆ‘ b in B, |âˆ‘ i, (f (a i) - f (b i))| ^ (m + 1)) / B.card := by
      gcongr; exact pow_sum_div_card_le_sum_pow _ _ fun _ _ â†¦ abs_nonneg _
    _ = _ := by simp [B]

private lemma step_one' (hA : A.Nonempty) (f : Î± â†’ â„) (hf : âˆ€ i, âˆ‘ a in A ^^ n, f (a i) = 0) (m : â„•)
    (a : n â†’ Î±) :
    |âˆ‘ i, f (a i)| ^ m â‰¤ (âˆ‘ b in A ^^ n, |âˆ‘ i, (f (a i) - f (b i))| ^ m) / A.card ^ card n := by
  cases m
  Â· simp [hA.ne_empty]
  exact step_one hA f a hf

private lemma step_two_aux (A : Finset Î±) (f : Î± â†’ â„) (Îµ : n â†’ â„)
    (hÎµ : Îµ âˆˆ ({-1, 1} : Finset â„)^^n) (g : (n â†’ â„) â†’ â„) :
    âˆ‘ a in A ^^ n, âˆ‘ b in A ^^ n, g (Îµ * (f âˆ˜ a - f âˆ˜ b)) =
      âˆ‘ a in A ^^ n, âˆ‘ b in A ^^ n, g (f âˆ˜ a - f âˆ˜ b) := by
  rw [â† sum_product', â† sum_product']
  let swapper (xy : (n â†’ Î±) Ã— (n â†’ Î±)) : (n â†’ Î±) Ã— (n â†’ Î±) :=
    (fun i â†¦ if Îµ i = 1 then xy.1 i else xy.2 i, fun i â†¦ if Îµ i = 1 then xy.2 i else xy.1 i)
  have hâ‚ : âˆ€ a âˆˆ (A ^^ n) Ã—Ë¢ (A ^^ n), swapper a âˆˆ (A ^^ n) Ã—Ë¢ (A ^^ n) := by
    simp only [mem_product, and_imp, mem_piFinset, â† forall_and]
    intro a h i
    split_ifs
    Â· exact h i
    Â· exact (h i).symm
  have hâ‚‚ : âˆ€ a âˆˆ (A ^^ n) Ã—Ë¢ (A ^^ n), swapper (swapper a) = a := fun a _ â†¦ by
    ext <;> simp only <;> split_ifs <;> rfl
  refine sum_nbij' swapper swapper hâ‚ hâ‚ hâ‚‚ hâ‚‚ ?_
  Â· rintro âŸ¨a, bâŸ© _
    congr with i : 1
    simp only [Pi.mul_apply, Pi.sub_apply, Function.comp_apply]
    simp only [mem_piFinset, mem_insert, mem_singleton] at hÎµ
    split_ifs with h
    Â· simp [h]
    rw [(hÎµ i).resolve_right h]
    ring

private lemma step_two (f : Î± â†’ â„) :
    âˆ‘ a in A ^^ n, âˆ‘ b in A ^^ n, (âˆ‘ i, (f (a i) - f (b i))) ^ (2 * m) =
      2â»Â¹ ^ card n * âˆ‘ Îµ in ({-1, 1} : Finset â„)^^n,
        âˆ‘ a in A ^^ n, âˆ‘ b in A ^^ n, (âˆ‘ i, Îµ i * (f (a i) - f (b i))) ^ (2 * m) := by
  let B := A ^^ n
  have : âˆ€ Îµ âˆˆ ({-1, 1} : Finset â„)^^n,
    âˆ‘ a in B, âˆ‘ b in B, (âˆ‘ i, Îµ i * (f (a i) - f (b i))) ^ (2 * m) =
      âˆ‘ a in B, âˆ‘ b in B, (âˆ‘ i, (f (a i) - f (b i))) ^ (2 * m) :=
    fun Îµ hÎµ â†¦ step_two_aux A f _ hÎµ fun z : n â†’ â„ â†¦ univ.sum z ^ (2 * m)
  rw [Finset.sum_congr rfl this, sum_const, card_piFinset, card_pair, prod_const, card_univ,
    nsmul_eq_mul, Nat.cast_pow, Nat.cast_two, inv_pow, inv_mul_cancel_leftâ‚€]
  Â· positivity
  Â· norm_num

private lemma step_three (f : Î± â†’ â„) :
    âˆ‘ Îµ in ({-1, 1} : Finset â„)^^n,
      âˆ‘ a in A ^^ n, âˆ‘ b in A ^^ n, (âˆ‘ i, Îµ i * (f (a i) - f (b i))) ^ (2 * m) =
      âˆ‘ a in A ^^ n, âˆ‘ b in A ^^ n, âˆ‘ k in piAntidiag univ (2 * m),
          (multinomial univ k * âˆ t, (f (a t) - f (b t)) ^ k t) *
            âˆ‘ Îµ in ({-1, 1} : Finset â„)^^n, âˆ t, Îµ t ^ k t := by
  simp only [@sum_comm _ _ (n â†’ â„) _ _ (A ^^ n), â† Complex.abs_pow, sum_pow_eq_sum_piAntidiag]
  refine sum_congr rfl fun a _ â†¦ ?_
  refine sum_congr rfl fun b _ â†¦ ?_
  simp only [mul_pow, prod_mul_distrib, @sum_comm _ _ (n â†’ â„), â† mul_sum, â† sum_mul]
  refine sum_congr rfl fun k _ â†¦ ?_
  rw [â† mul_assoc, mul_right_comm]

private lemma step_four {k : n â†’ â„•} :
    âˆ‘ Îµ in ({-1, 1} : Finset â„)^^n, âˆ t, Îµ t ^ k t = 2 ^ card n * ite (âˆ€ i, Even (k i)) 1 0 := by
  norm_num [sum_prod_piFinset _ fun i â†¦ (Â· ^ k i), neg_one_pow_eq_ite, ite_add,
    Fintype.prod_ite_zero]

private lemma step_six {f : Î± â†’ â„} {a b : n â†’ Î±} :
    âˆ‘ k : n â†’ â„• in piAntidiag univ m,
        (multinomial univ fun a â†¦ 2 * k a : â„) * âˆ i, (f (a i) - f (b i)) ^ (2 * k i) â‰¤
      m ^ m * (âˆ‘ i, (f (a i) - f (b i)) ^ 2) ^ m := by
  simp only [sum_pow_eq_sum_piAntidiag, mul_sum, â† mul_assoc, pow_mul]
  gcongr with k hk
  rw [mem_piAntidiag] at hk
  exact mod_cast multinomial_two_mul_le_mul_multinomial.trans (by rw [hk.1])

private lemma end_step {f : Î± â†’ â„} (hm : 1 â‰¤ m) (hA : A.Nonempty) :
    (âˆ‘ a in A ^^ n, âˆ‘ b in A ^^ n, âˆ‘ k in piAntidiag univ m,
      â†‘(multinomial univ fun i â†¦ 2 * k i) * âˆ t, (f (a t) - f (b t)) ^ (2 * k t)) / A.card ^ card n
        â‰¤ (4 * m) ^ m * âˆ‘ a in A ^^ n, (âˆ‘ i, f (a i) ^ 2) ^ m := by
  let B := A ^^ n
  calc
    (âˆ‘ a in B, âˆ‘ b in B, âˆ‘ k : n â†’ â„• in piAntidiag univ m,
      (multinomial univ fun i â†¦ 2 * k i : â„) * âˆ t, (f (a t) - f (b t)) ^ (2 * k t)) /
        A.card ^ card n
    _ â‰¤ (âˆ‘ a in B, âˆ‘ b in B, m ^ m * 2 ^ (m + (m - 1)) *
          ((âˆ‘ i, f (a i) ^ 2) ^ m + (âˆ‘ i, f (b i) ^ 2) ^ m) : â„) / A.card ^ card n := by
      gcongr; exact step_six.trans <| step_seven.trans step_eight
    _ = _ := by
      simp only [mul_add, sum_add_distrib, sum_const, nsmul_eq_mul, â† mul_sum]
      rw [â† mul_add, â† mul_add, â† two_mul, card_piFinset, prod_const, card_univ, Nat.cast_pow,
        mul_div_cancel_leftâ‚€ _ (by positivity), â† mul_assoc, mul_assoc _ _ 2, mul_comm 4, mul_pow,
        â† pow_succ, add_assoc, Nat.sub_add_cancel hm, â† two_mul, pow_mul]
      norm_num

namespace Real

/-- The **Marcinkiewicz-Zygmund inequality** for real-valued functions, with a slightly better
constant than `Real.marcinkiewicz_zygmund`. -/
theorem marcinkiewicz_zygmund' (m : â„•) (f : Î± â†’ â„) (hf : âˆ€ i, âˆ‘ a in A ^^ n, f (a i) = 0) :
    âˆ‘ a in A ^^ n, (âˆ‘ i, f (a i)) ^ (2 * m) â‰¤
      (4 * m) ^ m * âˆ‘ a in A ^^ n, (âˆ‘ i, f (a i) ^ 2) ^ m := by
  obtain rfl | hm := m.eq_zero_or_pos
  Â· simp
  have hm' : 1 â‰¤ m := by rwa [Nat.succ_le_iff]
  obtain rfl | hA := A.eq_empty_or_nonempty
  Â· cases isEmpty_or_nonempty n <;> cases m <;> simp
  let B := A ^^ n
  calc
    âˆ‘ a in B, (âˆ‘ i, f (a i)) ^ (2 * m)
      â‰¤ âˆ‘ a in A ^^ n, (âˆ‘ b in B, |âˆ‘ i, (f (a i) - f (b i))| ^ (2 * m)) / A.card ^ card n := by
      gcongr; simpa [pow_mul, sq_abs] using step_one' hA f hf (2 * m) _
    _ â‰¤ _ := ?_
  rw [â† sum_div]
  simp only [pow_mul, sq_abs]
  simp only [â† pow_mul]
  rw [step_two, step_three, mul_comm, inv_pow, â† div_eq_mul_inv, div_div]
  simp only [step_four, mul_ite, mul_zero, mul_one, â† sum_filter, â† sum_mul]
  rw [mul_comm, mul_div_mul_left _ _ (by positivity)]
  simpa only [even_iff_two_dvd, â† map_nsmul_piAntidiag_univ _ two_ne_zero, sum_map,
    Function.Embedding.coeFn_mk] using end_step hm' hA

/-- The **Marcinkiewicz-Zygmund inequality** for real-valued functions, with a slightly easier to
bound constant than `Real.marcinkiewicz_zygmund'`.

Note that `InnerProductSpace.marcinkiewicz_zygmund` and `RCLike.marcinkiewicz_zygmund` are other
versions that works for more general target spaces, at the expense of a slightly worse constant. -/
theorem marcinkiewicz_zygmund (hm : m â‰  0) (f : Î± â†’ â„) (hf : âˆ€ i, âˆ‘ a in A ^^ n, f (a i) = 0) :
    âˆ‘ a in A ^^ n, (âˆ‘ i, f (a i)) ^ (2 * m) â‰¤
      (4 * m) ^ m * card n ^ (m - 1) * âˆ‘ a in A ^^ n, âˆ‘ i, f (a i) ^ (2 * m) := by
  obtain _ | m := m
  Â· simp at hm
  obtain hn | hn := isEmpty_or_nonempty n
  Â· simp
  calc
    âˆ‘ a in A ^^ n, (âˆ‘ i, f (a i)) ^ (2 * (m + 1))
      â‰¤ (4 * â†‘(m + 1)) ^ (m + 1) * âˆ‘ a in A ^^ n, (âˆ‘ i, f (a i) ^ 2) ^ (m + 1) :=
      marcinkiewicz_zygmund' _ f hf
    _ â‰¤ (4 * â†‘(m + 1)) ^ (m + 1) * (âˆ‘ a in A ^^ n, card n ^ m * âˆ‘ i, f (a i) ^ (2 * (m + 1))) := ?_
    _ â‰¤ (4 * â†‘(m + 1)) ^ (m + 1) * card n ^ m * âˆ‘ a in A ^^ n, âˆ‘ i, f (a i) ^ (2 * (m + 1)) := by
      simp_rw [mul_assoc, mul_sum]; rfl
  gcongr with a
  rw [â† div_le_iff' (by positivity)]
  have := Real.pow_sum_div_card_le_sum_pow (f := fun i â†¦ f (a i) ^ 2) univ m fun i _ â†¦ by positivity
  simpa only [Finset.card_fin, pow_mul] using this

end Real

namespace InnerProductSpace
variable {ğ•œ : Type*} [NormedAddCommGroup ğ•œ] [InnerProductSpace â„ ğ•œ] [FiniteDimensional â„ ğ•œ]

open FiniteDimensional in
/-- The **Marcinkiewicz-Zygmund inequality** for functions valued in a real inner product space. -/
lemma marcinkiewicz_zygmund (hm : m â‰  0) (f : Î± â†’ ğ•œ) (hf : âˆ€ i, âˆ‘ a in A ^^ n, f (a i) = 0) :
    âˆ‘ a in A ^^ n, â€–âˆ‘ i, f (a i)â€– ^ (2 * m) â‰¤
      (4 * finrank â„ ğ•œ * m) ^ m * card n ^ (m - 1) * âˆ‘ a in A ^^ n, âˆ‘ i, â€–f (a i)â€– ^ (2 * m) := by
  obtain ht | ht := Nat.eq_zero_or_pos (finrank â„ ğ•œ)
  Â· rw [FiniteDimensional.finrank_zero_iff] at ht
    have : 2 * m â‰  0 := by positivity
    simp [norm_of_subsingleton, zero_pow this]
  let b := stdOrthonormalBasis â„ ğ•œ; clear_value b
  set B := A ^^ n
  simp only [pow_mul, â† b.repr.norm_map, PiLp.norm_sq_eq_of_L2, map_sum, norm_eq_abs, sq_abs,
    Fintype.sum_apply (Î³ := n)]
  set T := Fin (finrank â„ ğ•œ)
  calc
    âˆ‘ a in B, (âˆ‘ t : T, (âˆ‘ i, b.repr (f (a i)) t) ^ 2) ^ m
      = âˆ‘ a in B, card T ^ (m - 1)
          * ((âˆ‘ t : T, (âˆ‘ i, b.repr (f (a i)) t) ^ 2) ^ m / card T ^ (m - 1)) := by
      congr! with a
      have : 0 < card T := by simpa [T] using ht
      field_simp
    _ â‰¤ âˆ‘ a in B, card T ^ (m - 1) * (âˆ‘ t : T, ((âˆ‘ i, b.repr (f (a i)) t) ^ 2) ^ m) := by
      gcongr with a
      convert pow_sum_div_card_le_sum_pow (s := Finset.univ) (n := m - 1) ?_
      Â· rw [sub_one_add_one hm]
      Â· rw [sub_one_add_one hm]
      Â· intros; positivity
    _ = card T ^ (m - 1) * (âˆ‘ t : T, âˆ‘ a in B, (âˆ‘ i, b.repr (f (a i)) t) ^ (2 * m)) := by
      simp only [â† sum_add_distrib, mul_sum, pow_mul]
      rw [sum_comm]
    _ â‰¤ card T ^ (m - 1) * (âˆ‘ t : T, (4 * m) ^ m * card n ^ (m - 1) *
          âˆ‘ a in B, âˆ‘ i, b.repr (f (a i)) t ^ (2 * m)) := by
      gcongr with t
      apply Real.marcinkiewicz_zygmund hm (f := fun x â†¦ b.repr (f x) t)
      intro i
      rw [â† Finset.sum_apply, â† map_sum (g := b.repr), hf, map_zero, PiLp.zero_apply]
    _ = card T ^ (m - 1) * ((4 * m) ^ m * card n ^ (m - 1) *
          âˆ‘ a in B, âˆ‘ i, âˆ‘ t : T, (b.repr (f (a i)) t ^ (2 * m))) := by
      simp only [Finset.mul_sum, sum_comm (Î³ := T)]
    _ â‰¤ card T ^ (m - 1) * ((4 * m) ^ m * card n ^ (m - 1) *
          âˆ‘ a in B, âˆ‘ i, âˆ‘ t' : T, (âˆ‘ t : T, b.repr (f (a i)) t ^ 2) ^ m) := by
      simp_rw [pow_mul]
      gcongr with a _ i _ t' ht'
      apply single_le_sum (s := Finset.univ) (f := fun t â†¦ (b.repr (f (a i)) t) ^ 2) ?_ ht'
      intros
      positivity
    _ = card T ^ (m - 1) * ((4 * m) ^ m * card n ^ (m - 1) *
          âˆ‘ a in B, âˆ‘ i, card T * (âˆ‘ t : T, b.repr (f (a i)) t ^ 2) ^ m) := by simp
    _ = (4 * card T * m) ^ m * (card n) ^ (m - 1) *
        âˆ‘ a in B, âˆ‘ i, (âˆ‘ t : T, b.repr (f (a i)) t ^ 2) ^ m := by
      simp_rw [â† mul_sum, â† mul_assoc]
      congrm ?_ * _
      nth_rw 3 6 [â† sub_one_add_one hm]
      ring
    _ = _ := by simp [T]

end InnerProductSpace

namespace RCLike
variable {ğ•œ : Type*} [RCLike ğ•œ]

/-- The **Marcinkiewicz-Zygmund inequality** for real- or complex-valued functions. -/
lemma marcinkiewicz_zygmund (hm : m â‰  0) (f : Î± â†’ ğ•œ) (hf : âˆ€ i, âˆ‘ a in A ^^ n, f (a i) = 0) :
    âˆ‘ a in A ^^ n, â€–âˆ‘ i, f (a i)â€– ^ (2 * m) â‰¤
      (8 * m) ^ m * card n ^ (m - 1) * âˆ‘ a in A ^^ n, âˆ‘ i, â€–f (a i)â€– ^ (2 * m) := by
  let _ : InnerProductSpace â„ ğ•œ := RCLike.innerProductSpaceReal
  refine le_trans (InnerProductSpace.marcinkiewicz_zygmund hm f hf) ?_
  gcongr
  norm_cast
  linarith [RCLike.finrank_le_two ğ•œ]

end RCLike
